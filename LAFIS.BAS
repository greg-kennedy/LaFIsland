10 REM-LOST AND FORGOTTEN ISLAND
20 REM-REV. 6/8/84
30 DIM T$(12),T2(3,9),T3(3,15),V1(3,9),V2(3,9),N$(3),C1(4)
40 DIM V3(3,9),V4(3,9),C3(4),I$(2),J$(9),W(4),G1(4),G2(4)
50 DIM N3(2),B$(2)
60 REM INSTRUCTIONS
70 REM ADAPTED FROM "COMPUTERS AND SOCIETY" VOL.7-NO.3.FALL,1976
80 PRINT "WELCOME TO THE LOST AND FORGOTTEN ISLAND."
90 PRINT "WOULD YOU LIKE SOME INSTRUCTIONS";
100 INPUT Z$
110 IF Z$="YES" THEN 150
120 IF Z$="NO" THEN 250
130 PRINT "INVALID ANSWER, PLEASE RETYPE. YES OR NO"
140 GOTO 100
150 PRINT "LOST AND FORGOTTEN ISLAND IS A SURVIVAL GAME BASED ON"
160 PRINT "COOPERATION. IT CONTAINS A MIXTURE OF LIFE'S VALUES."
170 PRINT "IMAGINE:"
180 PRINT "         YOU HAVE BEEN SHIPWRECKED ON A REMOTE ISLAND."
190 PRINT "YOU HAVE THE CHOICE OF DIGGING FOR GOLD AND/OR BUILDING"
200 PRINT "A SHIP TO SURVIVE THE APPROACHING HURRICANE."
210 PRINT "CAN YOU SURVIVE?  IF SO, WITH HOW MUCH GOLD?"
220 PRINT
230 PRINT "      GOOD LUCK"
240 PRINT
250 T8=RND(1)
260 FOR I=1 TO 3
270 FOR J=1 TO 9
280 T2(I,J)=0
290 T3(I,J)=0
300 V1(I,J)=0
310 V2(I,J)=0
320 NEXT J
330 NEXT I
340 FOR I=1 TO 2
350 N3(I)=0
360 NEXT I
370 FOR I=1 TO 4
380 W(I)=0
390 G1(I)=0
400 G2(I)=0
410 C3(I)=0
420 C1(I)=0
430 NEXT I
440 N$(3)="STORAGE"
450 FOR I=1 TO 9
460 READ T$(I)
470 NEXT I
480 PRINT
490 REM NUMBER OF PLAYERS
500 PRINT "HOW MANY PEOPLE (1/2/3) ARE PLAYING";
510 FOR U1=1 TO 3
520 FOR U2=1 TO 9
530 T2(U1,U2)=0
540 NEXT U2
550 NEXT U1
560 INPUT N1
570 PRINT
580 IF N1<1 THEN 600
590 IF N1<=3 THEN 710
600 PRINT "YOU MUST PLAY WITH 1,2 OR 3 PLAYERS"
610 PRINT
620 GOTO 500
630 E=0
640 FOR H=1 TO N1
650 G1(H)=0
660 C3(H)=0
670 C1(H)=0
680 W(H)=0
690 NEXT H
700 C2=0
710 FOR I=1 TO N1
720 PRINT "PLAYER ";I;"WHAT NAME ARE YOU USING";
730 INPUT N$(I)
740 PRINT
750 FOR J=1 TO 3
760 IF J=I THEN 810
770 IF N$(I)<>N$(J) THEN 810
780 PRINT "SOMEONE ELSE ALREADY HAS THIS NAME SO PLEASE CHOOSE ANOTHER."
790 PRINT
800 GOTO 730
810 NEXT J
820 NEXT I
830 FOR U3=1 TO 3
840 FOR U4=1 TO 9
850 T2(U3,U4)=1
860 T2(U3,U4)=T2(U3,U4)*(-10)
870 NEXT U4
880 NEXT U3
890 REM
900 FOR I=1 TO 5
910 T3(1,I)=1
920 NEXT I
930 T3(1,6)=2
940 T3(2,7)=1
950 T3(2,8)=1
960 T3(2,9)=2
970 E=6-N1
980 FOR I=1 TO N1
990 FOR J=1 TO E
1000 R1=INT(RND(1)*9+1)
1010 T2(I,J)=R1
1020 V1(I,J)=T3(1,R1)
1030 V2(I,J)=T3(2,R1)
1040 NEXT J
1050 C1(I)=INT(RND(1)*11+2)
1060 NEXT I
1070 GOSUB 1110
1080 DATA "AXE","CHISEL","HAMMER","NAILS AND SCREWS","SAW"
1090 DATA "LUMBER","SHOVEL","PICKAXE","EXPLOSIVES"
1100 STOP
1110 REM SUBROUTINE LAFIS21
1120 REM THIS IS LAFIS 21
1130 REM TRADING TOOLS
1140 IF N1=1 THEN 1240
1150 IF N1=3 THEN 1170
1160 N$(3)="STORAGE"
1170 J$(1)="A"
1180 J$(2)="ANOTHER"
1190 FOR I=3 TO 9
1200 J$(I)=J$(I-1)
1210 NEXT I
1220 I$(1)="WHO (ONE NAME ONLY PLEASE) WISHES TO TRADE"
1230 I$(2)="WHO ELSE WISHES TO TRADE"
1240 S=4
1250 FOR M=1 TO 5
1260 REM WHICH DAY?
1270 PRINT "THIS IS DAY ";M
1280 PRINT
1290 GOSUB 3990
1300 PRINT
1310 PRINT
1320 IF S=1 THEN 3970
1330 IF S>3 THEN 1390
1340 S=S-1
1350 PRINT "THE STORM IS ABOUT TO HIT"
1360 PRINT
1370 IF N1=1 THEN 2910
1380 GOTO 1430
1390 IF M=3 THEN 1420
1400 X=INT(RND(1)*4+1)
1410 IF X<>4 THEN 1430
1420 S=3
1430 IF N1=1 THEN 2910
1440 PRINT "DO ANY OF YOU WISH TO TRADE TOOLS";
1450 INPUT A$
1460 PRINT
1470 IF A$="YES" THEN 1520
1480 IF A$="NO" THEN 2910
1490 PRINT "PLEASE TRY AGAIN. YOU MUST ANSWER YES OR NO."
1500 PRINT
1510 GOTO 1430
1520 FOR I=1 TO 3
1530 FOR J=1 TO 9
1540 T3(I,J)=T2(I,J)
1550 V3(I,J)=V1(I,J)
1560 V4(I,J)=V2(I,J)
1570 NEXT J
1580 G2(I)=G1(I)
1590 NEXT I
1600 FOR J=1 TO 2
1610 N3(J)=1
1620 NEXT J
1630 FOR I=1 TO 2
1640 PRINT I$(I);
1650 INPUT B$(I)
1660 PRINT
1670 IF B$(I)=N$(1) THEN 1760
1680 IF B$(I)=N$(2) THEN 1750
1690 IF B$(I)=N$(3) THEN 1740
1700 PRINT "YOU MUST ANSWER WITH '";N$(1);"', OR '";N$(3)
1710 PRINT "      PLEASE TRY AGAIN"
1720 PRINT
1730 GOTO 1640
1740 N3(I)=N3(I)+1
1750 N3(I)=N3(I)+1
1760 NEXT I
1770 FOR I=1 TO 2
1780 N4=1
1790 PRINT B$(I);", ARE YOU GIVING ANY GOLD IN THIS TRADE";
1800 INPUT C$
1810 PRINT
1820 IF C$="X" THEN 2830
1830 IF C$="NO" THEN 2080
1840 IF C$="YES" THEN 1950
1850 IF C$="T" THEN 1910
1860 PRINT "PLEASE TRY AGAIN. YOU MUST ANSWER YES, NO,"
1870 PRINT "X (TO CALL OFF THE TRADE), OR T (TO SEE THE LIST OF"
1880 PRINT "TOOLS WHICH EVERYONE HAD BEFORE THE START OF THIS TRADE)."
1890 PRINT
1900 GOTO 1790
1910 PRINT "YOUR SITUATION AT THIS TIME"
1920 PRINT
1930 GOSUB 4040
1940 GOTO 1790
1950 PRINT B$(I)", HOW MUCH GOLD (IN DOLLARS) ARE YOU GOING TO GIVE";
1960 INPUT A7
1970 PRINT
1980 IF G1(N3(I))>A7 THEN 2050
1990 PRINT "YOU MAY NOT GIVE MORE THAN YOU HAVE (";G1(N3(I));") DOLLARS"
2000 PRINT
2010 GOTO 1950
2020 IF A7>=0 THEN 2060
2030 PRINT "YOU MAY NOT INPUT A NEGATIVE NUMBER. TRY AGAIN."
2040 PRINT
2050 GOTO 1950
2060 G2(N3(I))=G1(N3(I))-A7
2070 G2(N3(3-I))=G1(N3(3-I))+A7
2080 PRINT B$(I);", ARE YOU GIVING A(NY) TOOL(S) IN THIS TRADE";
2090 INPUT D$
2100 PRINT
2110 IF D$="X" THEN 2830
2120 IF D$="NO" THEN 2680
2130 IF D$="YES" THEN 2220
2140 IF D$="T" THEN 2180
2150 PRINT "TRY AGAIN. ANSWER YES, NO, X OR T"
2160 PRINT
2170 GOTO 2080
2180 PRINT "YOUR SITUATION AT THIS TIME"
2190 PRINT
2200 GOSUB 4040
2210 GOTO 2080
2220 PRINT B$(I);", HOW MANY TOOLS ARE YOU GIVING";
2230 INPUT N5
2240 PRINT
2250 FOR J=1 TO N5
2260 PRINT B$(I);", WHAT IS THE NAME OF ";J$(J);" TOOL THAT ";
2270 PRINT "YOU ARE GIVING IN TRADE";
2280 INPUT E$
2290 PRINT
2300 IF E$="X" THEN 2830
2310 IF E$="B" THEN 2680
2320 IF E$="T" THEN 2530
2330 FOR R=1 TO 9
2340 IF T$(R)=E$ THEN 2430
2350 NEXT R
2360 PRINT "PLEASE USE THE NAME OF THE TOOL, USE B IF YOU WANT TO"
2370 PRINT "GO AHEAD WITH THE TRADE WITHOUT GIVING MORE TOOLS,"
2380 PRINT "USE T IF YOU WANT TO SEE THE LIST OF TOOLS EVERYONE"
2390 PRINT "HAD BEFORE THIS TRADE STARTED,"
2400 PRINT "OR USE X IF YOU WANT TO CALL OFF THE TRADE."
2410 PRINT
2420 GOTO 2260
2430 FOR K=1 TO 9
2440 IF T2(N3(I),K)=R THEN 2570
2450 NEXT K
2460 PRINT B$(I);", YOU DO NOT HAVE THIS TOOL.  PLEASE TRY AGAIN."
2470 PRINT "YOU MUST USE THE NAME OF A TOOL YOU HAVE, USE B TO GO"
2480 PRINT "AHEAD WITH THE TRADE WITHOUT GIVING MORE TOOLS, USE X"
2490 PRINT "TO CALL OFF THE TRADE, OR USE T TO SEE THE LIST OF TOOLS WHICH"
2500 PRINT "EVERYONE HAD BEFORE THE START OF THIS TRADE."
2510 PRINT
2520 GOTO 2260
2530 PRINT "YOUR SITUATION AT THIS TIME"
2540 PRINT
2550 GOSUB 4040
2560 GOTO 2260
2570 T3(N3(I),K)=-10
2580 V3(N3(I),K)=0
2590 V4(N3(I),K)=0
2600 FOR L=N4 TO 9
2610 IF T3(N3(3-I),L)=-10 THEN 2630
2620 NEXT L
2630 T3(N3(3-I),L)=R
2640 V3(N3(3-I),L)=V1(N3(I),K)
2650 V4(N3(3-I),L)=V2(N3(I),K)
2660 N4=L+1
2670 NEXT J
2680 NEXT I
2690 PRINT "THIS IS YOUR LAST CHANCE TO CALL OFF THE TRADE. IF YOU"
2700 PRINT "WANT TO CALL IT OFF TYPE X, OTHERWISE TYPE ANY OTHER LETTER AFTER"
2710 PRINT "THE QUESTION MARK."
2720 INPUT G$
2730 PRINT
2740 IF G$="X" THEN 2830
2750 FOR I=1 TO 3
2760 FOR J=1 TO 9
2770 T2(I,J)=T3(I,J)
2780 V1(I,J)=V3(I,J)
2790 V2(I,J)=V4(I,J)
2800 NEXT J
2810 G1(I)=G2(I)
2820 NEXT I
2830 PRINT "DO ANY TWO OF YOU WISH TO TRADE NOW";
2840 INPUT H$
2850 PRINT
2860 IF H$="YES" THEN 1520
2870 IF H$="NO" THEN 2910
2880 PRINT "PLEASE TRY AGAIN. YOU MUST ANSWER YES OR NO."
2890 PRINT
2900 GOTO 2830
2910 FOR I=1 TO N1
2920 IF C3(I)=1 THEN 3940
2930 P1=C1(I)
2940 REM WHAT TYPE OF WORK TODAY?
2950 PRINT N$(I);", WHAT ARE YOU GOING TO WORK ON TODAY";
2960 INPUT A$
2970 PRINT
2980 IF A$="BOAT" THEN 3370
2990 IF A$="GOLD" THEN 3050
3000 PRINT "PLEASE ANSWER BOAT IF YOU WANT TO WORK ON THE BOAT"
3010 PRINT "OR GOLD IF YOU WANT TO MINE GOLD."
3020 PRINT
3030 GOTO 2950
3040 REM WORKING ON SOME GOLD
3050 FOR J=1 TO 9
3060 IF T2(I,J)<>1 THEN 3220
3070 PRINT N$(I);", DO YOU WISH TO USE THE AXE TO MINE GOLD?"
3080 PRINT "REMEMBER THAT THE AXE DROPS GREATLY IN VALUE"
3090 PRINT "IF IT IS USED TO MINE GOLD."
3100 INPUT C$
3110 PRINT
3120 IF C$="NO" THEN 3220
3130 IF C$="YES" THEN 3170
3140 PRINT "PLEASE TRY AGAIN. YOU MUST USE YES OR NO."
3150 PRINT
3160 GOTO 3070
3170 V2(I,J)=V1(I,J)
3180 P1=P1+V2(I,J)*C1(I)
3190 V1(I,J)=(INT((V1(I,J)/2)*10+.5))/10
3200 V2(I,J)=0
3210 GOTO 3230
3220 P1=P1+V2(I,J)*C1(I)
3230 NEXT J
3240 Z8=RND(1)*2+1
3250 K=I
3260 IF C3(K)=1 THEN 3340
3270 C4=0
3280 IF K<>1 THEN 3300
3290 C4=.25
3300 C4=C4+.25
3310 Z9=C4*Z8*200*P1
3320 PRINT N$(K);" HAS JUST MADE ";INT(Z9);" DOLLARS MORE GOLD."
3330 G1(K)=G1(K)+Z9
3350 PRINT
3360 GOTO 3530
3370 REM WORKING ON THE BOAT
3380 B(I)=1
3390 FOR J=1 TO 9
3400 IF T2(I,J)<>2 THEN 3460
3410 FOR K=1 TO 9
3420 IF T2(I,K)<>3 THEN 3450
3430 P1=P1+2*V1(I,J)*C1(I)
3440 GOTO 3470
3450 NEXT K
3460 P1=P1+V1(I,J)*C1(I)
3470 NEXT J
3480 Z8=P1/12
3490 PRINT N$(I);" HAS EARNED ";INT(Z8);" MORE WORK POINTS."
3500 PRINT
3510 W(I)=W(I)+Z8
3520 C2=C2+Z8
3530 Y=INT(RND(1)*(9+C1(I))+1)
3540 FOR J=1 TO 9
3550 IF T2(I,J)=1 THEN 3580
3560 IF T2(I,J)=3 THEN 3580
3570 IF T2(I,J)<>8 THEN 3640
3580 IF Y<>T2(I,J) THEN 3930
3590 PRINT N$(I);" HAS BEEN INJURED BY THE ";T$(T2(I,J));". HIS/HER"
3600 PRINT "TOOL PROFICIENCY WILL NOW BE CUT IN HALF."
3610 PRINT
3620 C1(I)=INT((C1(I)/2)+.5)
3630 J=9
3640 IF T2(I,J)<>9 THEN 3930
3650 IF Y<>T2(I,J) THEN 3930
3660 PRINT N$(I);" HAS BEEN KILLED IN THE ACCIENTAL"
3670 PRINT "DISCHARGE OF SOME OF THE EXPLOSIVES. PLEASE"
3680 PRINT "NOTIFY HIS/HER FRIENDS AND RELATIVES IF YOU MAKE IT BACK."
3690 PRINT
3700 C3(I)=1
3710 IF D7=1 THEN 3740
3720 N$(I)="STORAGE"
3730 D7=1
3740 G1(I)=0
3750 G2(I)=0
3760 IF N1=1 THEN 3970
3770 FOR K=1 TO 9
3780 IF T2(I,K)=-10 THEN 3910
3790 R3=INT(RND(1)*N1+1)
3800 IF R3=I THEN 3850
3810 FOR L=1 TO 9
3820 IF T2(R3,L)<>-10 THEN 3900
3830 T2(R3,L)=T2(I,K)
3840 V1(R3,L)=V1(I,K)
3850 V2(R3,L)=V2(I,K)
3860 T2(I,K)=-10
3870 V1(I,K)=0
3880 V2(I,K)=0
3890 L=9
3900 NEXT L
3910 NEXT K
3920 J=9
3930 NEXT J
3940 NEXT I
3950 NEXT M
3960 GOSUB 3990
3970 GOSUB 4360
3980 REM THE FOLLOWING IS THE SUBROUTINE STATE
3990 FOR I=1 TO 3
4000 FOR J=1 TO 9
4010 T3(I,J)=T2(I,J)
4020 NEXT J
4030 NEXT I
4040 C4=C2
4050 FOR H=1 TO N1
4060 PRINT
4070 PRINT
4080 IF C3(H)=1 THEN 4270
4090 IF C2<>0 THEN 4110
4100 C2=1
4110 E=INT((W(H)/C2)*100+.5)
4120 PRINT N$(H);" HAS ";INT(G1(H));" DOLLARS WORTH OF GOLD, A TOOL"
4130 PRINT "PROFICIENCY OF ";C1(H);", ";INT(W(H));" WORK POINTS, WHICH"
4140 PRINT "IS ";E;" PERCENT OF THE TOTAL, AND THE FOLLOWING TOOLS:"
4150 PRINT
4160 FOR J2=1 TO 9
4170 K$=" "
4180 IF T2(H,J2)=-10 THEN 4220
4190 IF T2(H,J2)=T3(H,J2) THEN 4210
4200 K$="*"
4210 PRINT TAB(6);K$;T$(T2(H,J2))
4220 NEXT J2
4230 PRINT
4240 PRINT "JUST HIT RETURN WHEN YOU ARE READY TO GO ON.";
4250 INPUT F$
4260 PRINT
4270 NEXT H
4280 PRINT
4290 PRINT "THE SUM OF EVERYONE'S WORK POINTS IS ";INT(C4);"."
4300 PRINT
4310 C2=C4
4320 RETURN
4330 DATA "AXE","CHISEL","HAMMER","NAILS AND SCREWS","SAW"
4340 DATA "LUMBER","SHOVEL","PICKAXE","EXPLOSIVES"
4350 RETURN
4360 REM SUBROUTINE LAFIS 31
4370 DEF FNE(Z7)=2.718^Z7
4380 DEF FNC(Z8)=FNE(Z8)+FNE(-Z8)
4390 DEF FNS(Z9)=FNE(Z9)-FNE(-Z9)
4400 C4=C2
4410 IF C2<>0 THEN 4430
4420 C2=1
4430 IF Z1=3 THEN 4500
4440 C4=0
4450 FOR I=1 TO N1
4460 IF C3(I)=1 THEN 4480
4470 IF W(I)/C2<.25 THEN 4490
4480 C4=C4+W(I)
4490 NEXT I
4500 FOR I=1 TO N1
4510 IF C3(I)=1 THEN 4920
4520 PRINT
4530 PRINT
4540 REM THE RESULTS
4550 PRINT
4560 PRINT "THE RESULTS FOR ";N$(I);":"
4570 PRINT
4580 PRINT
4590 IF W(I)/C2<.25 THEN 4610
4600 W(I)=C4
4610 Z1=INT(60*2.718^((-W(I))/6))
4620 Z2=INT(50*(1+((FNS((7-W(I))/8.5))/FNC((7-W(I))/8.5))))
4630 Z3=INT(50*(1+(FNS((14-W(I))/5)/FNC((14-W(I))/5))))
4640 R5=INT(RND(1)*101)
4650 IF B(I)<>1 THEN 4850
4660 IF R5>Z1 THEN 4820
4670 PRINT "PROPER CONDOLENCES WILL BE SENT TO THE FRIENDS"
4680 PRINT "AND RELATIVES OF ";N$(I);" WHO DROWNED DURING"
4690 PRINT "TYPHOON URSULA."
4700 GOTO 4920
4710 IF R5>Z2 THEN 4860
4720 PRINT N$(I);", YOU MADE IT BACK TO HONOLULU BUT A"
4730 PRINT "LARGE WAVE WASHED YOUR GOLD OVERBOARD. SORRY."
4740 GOTO 4920
4750 IF R5>Z3 THEN 4920
4760 PRINT N$(I);", YOU MADE IT BACK BUT THE BOAT NEARLY SWAMPED."
4770 PRINT "SO, HALF OF YOUR GOLD WAS THROWN OVERBOARD."
4780 PRINT "THIS MEANS YOU HAVE";INT(G1(I)/2);
4790 PRINT " DOLLARS WORTH OF GOLD LEFT."
4800 GOTO 4920
4810 PRINT N$(I);", CONGRATULATIONS !"
4820 PRINT "YOU MADE IT WITH ALL YOUR GOLD,";INT(G1(1));
4830 PRINT " DOLLARS WORTH."
4840 GOTO 4920
4850 IF R5>=97 THEN 4890
4860 PRINT N$(I);", DID NOT GET OFF THE ISLAND AND WAS"
4870 PRINT "KILLED BY TYPHOON URSULA."
4880 GOTO 4920
4890 PRINT N$(I);", YOU SURVIVED TYPHOON URSULA, BUT LOST ALL YOUR GOLD"
4900 PRINT "AND HAD BETTER START MAKING SMOKE SIGNALS BECAUSE YOU WERE"
4910 PRINT "LEFT BEHIND."
4920 NEXT I
4930 PRINT
4940 PRINT "DO YOU WISH TO PLAY ANOTHER GAME";
4950 INPUT C$
4960 PRINT
4970 PRINT
4980 PRINT
4990 PRINT
5000 PRINT "************************"
5010 PRINT
5020 IF C$="NO" THEN 5080
5030 IF C$="YES" THEN 80
5040 PRINT "YOU MUST ANSWER YES OR NO. PLEASE TRY AGAIN."
5050 PRINT
5060 GOTO 4940
5070 RETURN
5080 END
